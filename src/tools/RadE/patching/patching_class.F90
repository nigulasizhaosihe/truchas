!!
!! PATCHING_CLASS
!!
!! A common interface to algorithms for clustering faces of a radiation
!! enclosure mesh.
!!
!! David Neill-Asanza <dhna@lanl.gov>
!! January 2021
!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!
!! This file is part of Truchas. 3-Clause BSD license; see the LICENSE file.
!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!
!! PROGRAMMING INTERFACE
!!
!! This module defines the abstract base class PATCHING which defines a common
!! interface to face clustering algorithms.  Application code is expected to use
!! polymorphic variables of this type and not work directly with extensions of
!! the type.  The base type defines The following type bound procedures
!!
!!   INIT(E, PARAMS, STAT, ERRMSG) initializes the object.  The ENCL object E is
!!     the radiation enclosure.  The PARAMETER_LIST object PARAMS gives the
!!     parameters associated with the patching algorithm; its expected content
!!     will depend on the dynamic type of the PATCHING object (see the comments
!!     for the extended types).  If an error is encountered, STAT returns a
!!     non-zero value and ERRMSG an explanatory message.
!!
!!   RUN(STAT, ERRMSG) executes the patching algorithm.  At the end of this
!!     procedure, the object's internal data will hold a valid patching of the
!!     enclosure.  This procedure can only be called once per object instance
!!     and must be called after the object is initialized.  If an error is
!!     encountered, STAT returns a non-zero value and ERRMSG an explanatory
!!     message.
!!
!!   OUTPUT(F2P_MAP, GLOBAL_IDS, NPATCH) returns the enclosure patch data
!!     stored in the object.  NPATCH is an integer scalar defining the total
!!     number of patches generated by RUN.  GLOBAL_IDS is a rank-1 integer array
!!     that defines the global ID of each patch.  Chaparral requires the
!!     GLOBAL_IDS array to define an enclosure.  Here, we simply number the
!!     patches consecutively from 1 (i.e. GLOBAL_IDS(j)=j).  F2P_MAP is a rank-1
!!     integer array that maps enclosure faces to the global ID of their patch.
!!     This procedure deallocates internal data and therefore must only be
!!     executed once.  This procedure must be called after RUN.
!!


module patching_class

  use re_encl_type
  use parameter_list_type
  implicit none
  private

  type, abstract, public :: patching
    contains
      procedure(init), public, deferred  :: init
      procedure(run), public, deferred  :: run
      procedure(output), public, deferred  :: output
  end type

  abstract interface
    subroutine init(this, e, params, stat, errmsg)
      import :: patching, parameter_list, encl
      class(patching), intent(out) :: this
      type(encl), intent(in), target :: e
      type(parameter_list), intent(inout) :: params
      integer, intent(out) :: stat
      character(:), allocatable, intent(out) :: errmsg
    end subroutine

    subroutine run(this, stat, errmsg)
      import :: patching
      class(patching), target, intent(inout) :: this
      integer, intent(out) :: stat
      character(:), allocatable, intent(out) :: errmsg
    end subroutine

    subroutine output(this, f2p_map, global_ids, npatch)
      import :: patching
      class(patching), intent(inout) :: this
      integer, allocatable, intent(out) :: f2p_map(:), global_ids(:)
      integer, intent(out) :: npatch
    end subroutine
  end interface

end module patching_class
